type Matcher = fn (&[u8], i32) -> (bool, i32);

fn @empty() -> Matcher = @|_, i| (true, i)
fn @any()   -> Matcher = @|_, i| (true, i + 1)

fn @one(c: u8) -> Matcher = @|str, i| {
    if str(i) == c {
        (true, i + 1)
    } else {
        (false, i)
    }
}

fn @maybe(e: Matcher) -> Matcher = @|str, i| {
    let (_, j) = e(str, i);
    (true, j)
}

fn @many(e: Matcher) -> Matcher = @|str, i| {
    let mut j = i;
    while true {
        let (ok, k) = e(str, j);
        if !ok { break() }
        j = k;
    }
    (true, j)
}

fn @and(a: Matcher, b: Matcher) -> Matcher = @|str, i| {
    let (ok_a, j) = a(str, i);
    if !ok_a { return(false, j) }
    b(str, j)
}

fn @or(a: Matcher, b: Matcher) -> Matcher = @|str, i| {
    let (ok_a, j) = a(str, i);
    if ok_a { return(true, j) }
    b(str, j)
}

fn @many1(e: Matcher) = and(e, many(e))

fn @parse(s: &[u8]) {
    fn @(?c & ?i) eat(c: u8, cur: Matcher, prev: Matcher, i: i32) -> (Matcher, i32) {
        match c {
            '(' => {
                let (next, j) = eat(s(i + 1), empty(), empty(), i + 1);
                eat(s(j), and(cur, prev), next, j)
            },
            ')' => { (and(cur, prev), i + 1) },
            0   => { (and(cur, prev), i)     },
            '+' => { let next = many1(prev); eat(s(i + 1), cur, next, i + 1) },
            '*' => { let next = many(prev);  eat(s(i + 1), cur, next, i + 1) },
            '?' => { let next = maybe(prev); eat(s(i + 1), cur, next, i + 1) },
            '.' => { let next = any();       eat(s(i + 1), and(cur, prev), next, i + 1) },
            _   => { let next = one(c);      eat(s(i + 1), and(cur, prev), next, i + 1) }
        }
    }
    let (matcher, _) = eat(s(0), empty(), empty(), 0);
    matcher
}

fn test(str: &[u8]) = parse("a*b+")(str, 0)
